buildscript {
    repositories {
        mavenCentral()
        maven {
            name = "forge"
            url = "https://maven.minecraftforge.net"
        }
        maven {
            name = "sponge"
            url = "https://repo.spongepowered.org/maven"
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:3.+'
        classpath 'org.spongepowered:mixingradle:0.7+'
    }
}
apply plugin: 'net.minecraftforge.gradle'
apply plugin: 'org.spongepowered.mixin'


version = "1.1"
group = "net.jay113355"
archivesBaseName = "IpForward"

minecraft {
    mappings channel: 'snapshot', version: '20171007-1.12'

    accessTransformers = files('src/main/resources/META-INF/ipforward_at.cfg').asList()

    runs {
        client {
            workingDirectory project.file('run')
        }
        server {
            workingDirectory project.file('runserver')
            args 'nogui'
        }
    }
}

repositories {
    mavenCentral()
    maven {
        name = "sponge"
        url = "https://repo.spongepowered.org/maven"
    }
}

dependencies {
    minecraft ('net.minecraftforge:forge:1.12.2-14.23.5.2860') {
        exclude group: 'org.scala-lang'
        exclude group: 'org.scala-lang.plugins'
    }

    compile("org.spongepowered:mixin:0.8")
}

processResources {
    // this will ensure that this task is redone when the versions change.
    inputs.property "version", project.version
    inputs.property "mcversion", '1.12.2'

    // replace stuff in mcmod.info, nothing else
    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
        expand 'version':project.version, 'mcversion':'1.12.2'
    }
}

jar {
    manifest.attributes(
            'FMLAT': "ipforward_at.cfg",
            'FMLCorePlugin': "net.jay113355.ipforward.IpForwardFML",
            'MixinConfigs': "mixins.ipforward.json",
    )
    from "LICENSE.txt"
}

mixin {
    add sourceSets.main, "mixins.ipforward.refmap.json"
}
